resource "azurerm_resource_group" "main" {
  name     = local.resource_group_name
  location = var.resource_group_location
}

module "network" {
  source                  = "./modules/network"
  resource_group_name     = azurerm_resource_group.main.name
  resource_group_location = azurerm_resource_group.main.location

  virtual_network_name          = "vnet"
  virtual_network_address_space = ["10.0.0.0/24"]                    # 256 ip addresses
  dns_servers                   = ["168.63.129.16", "168.63.129.17"] # Azure-provided DNS servers
  subnets = {
    "subnet1" = {
      address_prefixes = ["10.0.0.0/28"] # 16 ip addresses
    }
  }
}

module "nsg" {
  source                      = "./modules/nsg"
  resource_group_name         = azurerm_resource_group.main.name
  resource_group_location     = azurerm_resource_group.main.location
  network_security_group_name = "nsg"
  network_security_rules = {
    "https" = {
      priority                   = 100
      direction                  = "Inbound"
      access                     = "Allow"
      protocol                   = "Tcp"
      source_port_range          = "*"
      destination_port_range     = "443"
      source_address_prefix      = "10.0.0.0/24"
      destination_address_prefix = "*"
    },
  }
}

resource "azurerm_network_interface" "main" {
  name                = "nic"
  resource_group_name = azurerm_resource_group.main.name
  location            = azurerm_resource_group.main.location

  ip_configuration {
    name                          = "ipconfig"
    subnet_id                     = module.network.subnet_id_map["subnet1"]
    private_ip_address_allocation = "Dynamic"
  }

  depends_on = [module.network]
}

resource "azurerm_public_ip" "main" {
  name                = "publicip"
  resource_group_name = azurerm_resource_group.main.name
  location            = azurerm_resource_group.main.location
  allocation_method   = "Static"
  sku                 = "Standard"
}

resource "azurerm_lb" "main" {
  name                = "loadbalancer"
  resource_group_name = azurerm_resource_group.main.name
  location            = azurerm_resource_group.main.location

  frontend_ip_configuration {
    name                 = "PublicIPAddress"
    public_ip_address_id = azurerm_public_ip.main.id
  }

  depends_on = [azurerm_public_ip.main]
}

resource "azurerm_network_interface_security_group_association" "main" {
  network_interface_id      = azurerm_network_interface.main.id
  network_security_group_id = module.nsg.network_security_group_id

  depends_on = [
    azurerm_network_interface.main,
    module.nsg
  ]
}

output "publicip" {
  value = azurerm_public_ip.main.ip_address
}